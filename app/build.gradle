apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt' // https://kotlinlang.org/docs/reference/kapt.html

android {
    compileSdkVersion rootProject.ext.androidCompileSdkVersion
    defaultConfig {
        applicationId "com.hossainkhan.android.constraintlayout"
        minSdkVersion rootProject.ext.androidMinSdkVersion
        targetSdkVersion rootProject.ext.androidTargetSdkVersion
        versionCode 200
        versionName "2.0-constraint-layout-demo"
        versionNameSuffix "-${gitSha()}"

        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility rootProject.ext.javaSourceCompatibilityVersion
        targetCompatibility rootProject.ext.javaTargetCompatibilityVersion
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    dataBinding {
        // https://developer.android.com/topic/libraries/data-binding/start
        enabled = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // Kotlin support
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // Android Support libs and Google Android libs
    implementation "androidx.appcompat:appcompat:$rootProject.supportAppCompatVersion"
    implementation "androidx.legacy:legacy-support-v13:$rootProject.supportLibraryVersion"
    implementation "com.google.android.material:material:$rootProject.supportLibraryVersion"
    implementation "androidx.cardview:cardview:$rootProject.supportLibraryVersion"

    implementation "androidx.constraintlayout:constraintlayout:$rootProject.constraintLayoutVersion"

    // ViewModel and LiveData
    // https://developer.android.com/topic/libraries/architecture/adding-components
    // https://developer.android.com/jetpack/androidx/releases/lifecycle
    implementation "androidx.lifecycle:lifecycle-extensions:$rootProject.archComponentVersion"

    // https://developer.chrome.com/multidevice/android/customtabs
    implementation "androidx.browser:browser:$rootProject.supportLibraryVersion"

    // ========================================================
    // 3rd party libraries
    // ========================================================

    // Leak Canary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$rootProject.leakcanaryLibraryVersion"

    // Timber
    implementation "com.jakewharton.timber:timber:$rootProject.timberLibraryVersion"

    // Crashlytics
    // https://firebase.google.com/docs/crashlytics/get-started
    implementation "com.crashlytics.sdk.android:crashlytics:$rootProject.crashlyticsVersion"

    // Firebase Core
    // https://firebase.google.com/docs/android/setup
    implementation "com.google.firebase:firebase-core:$rootProject.firebaseVersion"
    // Firestore cloud realtime database
    // https://firebase.google.com/docs/firestore/quickstart?authuser=0
    implementation "com.google.firebase:firebase-firestore:$rootProject.firestoreVersion"

    // Dagger
    // https://google.github.io/dagger/
    implementation "com.google.dagger:dagger:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$rootProject.daggerVersion"
    implementation "com.google.dagger:dagger-android:$rootProject.daggerVersion"
    // Include if you use the support libraries (eg. Support Activity, Fragment)
    //implementation "com.google.dagger:dagger-android-support:$rootProject.daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$rootProject.daggerVersion"

    // ----------------------------------------------------------------
    // Android Unit and Instrumentation test
    // ----------------------------------------------------------------
    // Required -- JUnit 4 framework
    testImplementation "junit:junit:$rootProject.junitVersion"
    // Mocking framework for unit tests written in Java
    testImplementation "org.mockito:mockito-core:$rootProject.mockitoVersion"

    androidTestImplementation("androidx.test.espresso:espresso-core:$rootProject.espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation 'androidx.test:runner:1.2.0'
    testImplementation "androidx.arch.core:core-testing:$rootProject.archComponentVersion"
}

// ADD THIS AT THE BOTTOM
// https://firebase.google.com/docs/android/setup
apply plugin: 'com.google.gms.google-services'